Функция Печать(ИмяМакета = "", ПараметрыПечати = Неопределено)	Экспорт

	Если НЕ ЗначениеЗаполнено(СсылкаНаОбъект) Тогда
		Возврат Неопределено;
	КонецЕсли;
	
	Если Не СсылкаНаОбъект.Проведен Тогда
		Предупреждение("Недостаточно полномочий для печати непроведенного документа!");
		Возврат Ложь;
	КонецЕсли;
	
	ТабДокумент = ПечатьДокумента(СсылкаНаОбъект);
	
	//Определим параметры печати
	Если ПараметрыПечати <> Неопределено Тогда
		КоличествоЭкземпляров = 1;
		Если НЕ(ПараметрыПечати.Свойство("КоличествоЭкземпляров",КоличествоЭкземпляров)) Тогда
			КоличествоЭкземпляров = 1;			
		КонецЕсли;
		НаПринтер = Ложь;
		Если НЕ(ПараметрыПечати.Свойство("НаПринтер",НаПринтер)) Тогда
			НаПринтер = Ложь;			
		КонецЕсли;
	КонецЕсли;
	Если КоличествоЭкземпляров = Неопределено Тогда
		КоличествоЭкземпляров = 1;
	КонецЕсли;
	Если НаПринтер = Неопределено Тогда
		НаПринтер = Ложь;
	КонецЕсли;
	
	Возврат ТабДокумент;

КонецФункции // Печать()

Функция ПечатьДокумента(СсылкаНаДокумент)
#Область Инициализация
	МакетWord=ПолучитьМакет("Макет");
	MSWord=МакетWord.Получить();
	Попытка 
		Документ=MSWord.Application.Documents(1);
	Исключение
		Сообщить("Не удалость открыть печатную форму по причине "+ОписаниеОшибки(),СтатусСообщения.Важное); 
		Возврат Неопределено;
	КонецПопытки;
#КонецОбласти

	ЗаменитьШаблон(Документ,"[ДатаДок]",Формат(СсылкаНаДокумент.Дата,"ДФ='dd MMMM yyyy ""г.""'"));
	ЗаменитьШаблон(Документ,"[ОрганизацияПолностью]",СокрЛП(СсылкаНаДокумент.Организация.НаименованиеПолное));
	ЗаменитьШаблон(Документ,"[ОрганизацияСокр]",СокрЛП(СсылкаНаДокумент.Организация.НаименованиеСокращенное));
	
	ФИО_Директора=ПолучитьДиректора(СсылкаНаДокумент);
	ФИО_РП="";
	Если Не УниверсальныеМеханизмы.ПросклонятьФамилиюИмяОтчество(глЗначениеПеременной("глКомпонентаСклоненияФИО"), ФИО_Директора.Фамилия, ФИО_Директора.Имя, ФИО_Директора.Отчество,2,,ФИО_РП) Тогда
		ФИО_РП=ФИО_Директора.Фамилия+" "+ФИО_Директора.Имя+" "+ФИО_Директора.Отчество;
	КонецЕсли;
	ЗаменитьШаблон(Документ,"[Директор_РП]",ФИО_РП);
	
	Отбор=Новый Структура("ФизЛицо",СсылкаНаДокумент.РаботникиОрганизации[0].ФизЛицо);
	ФИО_Сотрудника=РегистрыСведений.ФИОФизЛиц.ПолучитьПоследнее(СсылкаНаДокумент.Дата,Отбор);
	ЗаменитьШаблон(Документ,"[Сотрудник_ФИО]",ФИО_Сотрудника.Фамилия+" "+ФИО_Сотрудника.Имя+" "+ФИО_Сотрудника.Отчество);
	
	ЗаменитьШаблон(Документ,"[Директор_ФамИО]",ОбщегоНазначенияЗК.ФамилияИнициалыФизЛица(,ФИО_Директора.Фамилия,ФИО_Директора.Имя,ФИО_Директора.Отчество));
	ЗаменитьШаблон(Документ,"[Сотрудник_ФамИО]",ОбщегоНазначенияЗК.ФамилияИнициалыФизЛица(,ФИО_Сотрудника.Фамилия,ФИО_Сотрудника.Имя,ФИО_Сотрудника.Отчество));
#Область Открытие_документа
	MSWord.Application.Visible=1;
	MSWord.Application.ActiveWindow.Visible=1;
	Документ.Activate();
	Документ.Application.Activate();
#КонецОбласти
	Возврат Неопределено;
КонецФункции

Процедура ЗаменитьШаблон(ДокWord, Шаблон, Значение)
	Замена=ДокWord.Content.Find;
	//
	Пока Замена.Execute(Шаблон, Ложь, Истина, Ложь,,, Истина,, Ложь, Значение) Цикл
		Замена=ДокWord.Content.Find;
	КонецЦикла;
КонецПроцедуры

// Процедура заполняет таблицу документа Word
//	ОбъектWord - СОМОбъект - документ Word
//	ВыборкаДанных - выборка запроса или ТаблицаЗначений - собственно чем заполняем
//	ПараметрШаблона - строка формата $$****&& - для поиска и позиционирования на таблице документа. Заполняться будет следующая за строкой параметра таблица.
//	СтруктураПолей - структура - Порядок и количество ключей должно соответствовать порядку и количеству колонок в таблице документа. 
//									Ключ - имя поля в выборке "ВыборкаДанных" для заполнения ячейки
//									Значение - строка - заголовок шаки таблицы, не обязателен, работает только с параметром ЗаполнятьШапку = ИСТИНА
//									Для пропуска колонки используем ключи формата _НеЗаполняемХХ (где хх - произвольные символы или число на случай если пропускаемых колонок несколько).
//	НомерПервойСтроки - число - номер первой строки для заполнения, по умолчанию = 2 т.к. считаем, что первая строка это шапка таблицы
//	ЗаполнятьШапку - булево - признак заполнения шапки, если ИСТИНА, то первая строка таблицы заполняется значениями из структуры "СтруктураПолей" 
//									Для пропуска колонки используем ключи формата _НеЗаполняемШапкуХХ, (!!ячеки данных будут так же пропущены!!), если же ключ формата _НеЗаполняемХХ, то ячейка шапки заполняется.
Процедура ЗаполнитьТаблицуДокумента(ОбъектWord, ВыборкаДанных, ПараметрШаблона, СтруктураПолей, НомерПервойСтроки = 2, ЗаполнятьШапку = Ложь) Экспорт
	
	Если ПустаяСтрока(ПараметрШаблона) Тогда
		Возврат;
	КонецЕсли;
	ВыделенныеДанные = ОбъектWord.ActiveWindow.Selection;
	ВыделенныеДанные.GoTo(-1,, , "Nachalo");//переходим в начало документа
	ВыделенныеДанные.Collapse();
	ОбъектПоиск 				= ВыделенныеДанные.Find;
	ОбъектПоиск.Forward 		= True;
	ОбъектПоиск.MatchWildcards 	= False;
	ОбъектПоиск.Text 			= ПараметрШаблона;
	Пока ОбъектПоиск.Execute() Цикл
		ВыделенныеДанные.Delete();
		ВыделенныеДанные.InsertAfter("");			
		ВыделенныеДанные.GoTo(2, 2, 1, "").Select();//выделяем первую таблицу после параметра
		Попытка
			ТаблицаWord = ВыделенныеДанные.Tables(1);//позиционируемся на таблице в выделении
		Исключение
			Сообщить(" - за параметром "+ПараметрШаблона+" не найдено ни одной таблицы!", СтатусСообщения.Внимание);
			Возврат;
		КонецПопытки;
		
		Если ТаблицаWord.Columns.Count+1 < СтруктураПолей.Количество() Тогда
			Сообщить(" - таблица шаблона "+" содержит "+(ТаблицаWord.Columns.Count()+1)+" колонок, из необходимых "+СтруктураПолей.Количество()+"! Таблица пропущена.", СтатусСообщения.Внимание);
			Возврат;
		КонецЕсли;
		
		СтрокТаблицы = ТаблицаWord.Rows.Count();
		Если СтрокТаблицы < НомерПервойСтроки Тогда
			ДобавитьСтрок = НомерПервойСтроки - СтрокТаблицы;
			ТаблицаWord.Rows(СтрокТаблицы).Select();
			ВыделенныеДанные.InsertRowsBelow(ДобавитьСтрок);
			СтрокТаблицы = ТаблицаWord.Rows.Count();
		КонецЕсли;
		
		Если ЗаполнятьШапку Тогда
			индексКолонки = 1;
			Для каждого ЗаполняемаяКолонка Из СтруктураПолей Цикл
				индексКолонки = индексКолонки + 1;
				Если Найти(НРег(ЗаполняемаяКолонка.Ключ), "_незаполняемшапку") = 0 Тогда
					ТаблицаWord.Cell(1, индексКолонки).Range.Text = Строка(ЗаполняемаяКолонка.Значение);
				КонецЕсли;
			КонецЦикла;
		КонецЕсли;
		ТаблицаWord.Rows(СтрокТаблицы).Select();
		
		ЭтоТаблицаЗначений = (ТипЗнч(ВыборкаДанных) = Тип("ТаблицаЗначений"));
		
		НомерСтроки = НомерПервойСтроки;// по умолчанию = 2, т.к. первая строка - шапка
		н = 0;
		КоличествоСтрокДанных = ВыборкаДанных.Количество();
		Для н = 0 По КоличествоСтрокДанных-1 Цикл
			
			Если ЭтоТаблицаЗначений Тогда
				Данные = ВыборкаДанных[н];
			Иначе
				ВыборкаДанных.Следующий();
				Данные = ВыборкаДанных;
			КонецЕсли;
			Если НомерСтроки > СтрокТаблицы Тогда
				ТаблицаWord.Rows(НомерСтроки-1).Select();
				ВыделенныеДанные.InsertRowsBelow();
			КонецЕсли;
			индексКолонки = 1;
			Для каждого ЗаполняемаяКолонка Из СтруктураПолей Цикл
				индексКолонки = индексКолонки + 1;
				Если Найти(НРег(ЗаполняемаяКолонка.Ключ), "_незаполняем") = 0 Тогда
					Если ТипЗнч(Данные[ЗаполняемаяКолонка.Ключ])=Тип("Число") Тогда
						ТаблицаWord.Cell(НомерСтроки, индексКолонки).Range.Text = Формат(Данные[ЗаполняемаяКолонка.Ключ],"ЧДЦ=2; ЧН=-");
					Иначе
						Если НЕ ЗначениеЗаполнено(Данные[ЗаполняемаяКолонка.Ключ]) Тогда
							ТаблицаWord.Cell(НомерСтроки, индексКолонки).Range.Text = "Для всех";
						Иначе
							ТаблицаWord.Cell(НомерСтроки, индексКолонки).Range.Text = Строка(Данные[ЗаполняемаяКолонка.Ключ]);
						КонецЕсли;
					КонецЕсли;
				КонецЕсли;
			КонецЦикла;
			НомерСтроки = НомерСтроки + 1;
			
		КонецЦикла;
	КонецЦикла;
	
КонецПроцедуры

Функция ПолучитьДиректора(СсылкаНаДокумент)
	Запрос = Новый Запрос;
	Запрос.Текст = 
		"ВЫБРАТЬ
		|	ФИОФизЛицСрезПоследних.ФизЛицо,
		|	ФИОФизЛицСрезПоследних.Фамилия,
		|	ФИОФизЛицСрезПоследних.Имя,
		|	ФИОФизЛицСрезПоследних.Отчество
		|ИЗ
		|	РегистрСведений.ОтветственныеЛицаОрганизаций.СрезПоследних(
		|			&ДатаСреза,
		|			СтруктурнаяЕдиница = &Организация
		|				И ОтветственноеЛицо = ЗНАЧЕНИЕ(Перечисление.ОтветственныеЛицаОрганизаций.Руководитель)) КАК ОтветственныеЛицаОрганизацийСрезПоследних
		|		ВНУТРЕННЕЕ СОЕДИНЕНИЕ РегистрСведений.ФИОФизЛиц.СрезПоследних(&ДатаСреза, ) КАК ФИОФизЛицСрезПоследних
		|		ПО ОтветственныеЛицаОрганизацийСрезПоследних.ФизическоеЛицо = ФИОФизЛицСрезПоследних.ФизЛицо";
	
	Запрос.УстановитьПараметр("ДатаСреза", СсылкаНаДокумент.Дата);
	Запрос.УстановитьПараметр("Организация", СсылкаНаДокумент.Организация);
	
	РезультатЗапроса = Запрос.Выполнить();
	
	ВыборкаДетальныеЗаписи = РезультатЗапроса.Выбрать();
	
	СтрВозврта=Новый Структура("ФизЛицо,Фамилия,Имя,Отчество");
	Если ВыборкаДетальныеЗаписи.Следующий() Тогда
		ЗаполнитьЗначенияСвойств(СтрВозврта,ВыборкаДетальныеЗаписи);
	КонецЕсли;
	
	Возврат СтрВозврта;
КонецФункции